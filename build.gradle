buildscript {
	repositories {
		maven {
			url "https://repo1.maven.org/maven2"
		}
		maven {
			name = "forge"
			url = "https://maven.minecraftforge.net/"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
		maven {
			url = "https://jitpack.io"
		}
		maven {
			url = "http://www.ryanliptak.com/maven/"
		}

	}
	dependencies {
		classpath 'com.github.GTNewHorizons:ForgeGradle:1.2.11'
	}
}

plugins {
	id "com.wynprice.cursemaven" version "2.1.4"
}

apply plugin: 'forge'

file "build.properties" withReader {
	def prop = new Properties()
	prop.load(it)
	ext.config = new ConfigSlurper().parse prop
}

version = "${config.minecraft.version}-${config.computronics.version}-Ctyrk4-v${config.ctyrk4.version}"
group = "pl.asie.computronics" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Computronics"

if (JavaVersion.current().isJava8Compatible()) {
	allprojects {
		tasks.withType(Javadoc) {
			options.addStringOption('Xdoclint:none', '-quiet')
		}
	}
}

repositories {
	maven {
		name = "OC repo"
		url = "http://maven.cil.li/"
	}
	maven {
		name "Mobius Repo"
		url "http://mobiusstrip.eu/maven"
	}
	maven {
		name 'CB Repo'
		url "http://chickenbones.net/maven/"
	}
	maven {
		name 'tterrag Repo'
		url "http://maven.tterrag.com/"
	}
	maven {
		name = "Forestry and IC2 repo"
		url = "http://maven2.ic2.player.to/"
	}
	maven {
		name = "GregTech"
		url = "http://gregtech.overminddl1.com/"
	}
	maven {
		name = "CurseForge"
		url = "https://minecraft.curseforge.com/api/maven/"
	}
	ivy {
		name = "BuildCraft"
		artifactPattern "http://www.mod-buildcraft.com/releases/BuildCraft/[revision]/[module]-[revision]-[classifier].[ext]"
	}
	flatDir {
		dirs "lib"
	}
	/*maven {
		name 'MaryTTS repo'
		url 'http://dl.bintray.com/marytts/marytts'
	}*/
}

configurations {
	embedded
	implementation.extendsFrom embedded
	all*.exclude group: "codechicken"
}

dependencies {
	implementation "li.cil.oc:OpenComputers:${config.oc.version}:dev"
	implementation "li.cil.tis3d:TIS-3D:${config.tis3d.version}:dev"
	compileOnly "net.industrial-craft:industrialcraft-2:${config.ic2.version}:dev"
	compileOnly "com.gregoriust.gregtech:gregtech_1.7.10:${config.gregtech.version}:dev"
	compileOnly "com.gregoriust.gregtech:gregtech_1.7.10:${config.gregtech6.version}:dev"
	implementation "mcp.mobius.waila:Waila:${config.waila.version}:dev"
	implementation "com.enderio.core:EnderCore:${config.endercore.version}:dev"
	implementation "mod.industrialcraft2:ic2"
	implementation "mod.forestry.api:forestry_1.7.10-4.4.0.0-dev"
	implementation "mod.net.bdew.gendustry.api:gendustry"
	implementation "mod.net.bdew.lib:bdew"
	implementation "mod.gregtech.api:gregtech"
	implementation "mod.buildcraft.api:buildcraft"
	implementation "mod.thaumcraft.api:Thaumcraft"
	implementation "mod.railcraft:Railcraft_1.7.10-9.12.2.0-dev"
	implementation("com.enderio:EnderIO:${config.enderio.version}:dev") {
		exclude module: "appliedenergistics2"
		exclude module: "buildcraft"
		exclude module: "Waila"
		exclude module: "forestry_1.7.10"
		exclude module: "OpenComputers"
		exclude module: "CodeChickenLib"
		exclude module: "CodeChickenCore"
		exclude module: "EquivalentExchange3"
		exclude module: "EnderCore"
	}
	compileOnly('curse.maven:openperipheralcore-228817:2386737') {
		transitive = false
	}
	compile "applecore:AppleCore:1.7.10-3.1.1:deobf"
	//compileOnly("com.bluepowermod:BluePower:${config.bluepower.version}:deobf") {
	//	transitive = false
	//}
	//compileOnly("qmunity:QmunityLib:${config.qmunitylib.version}:deobf") {
	//	transitive = false
	//}
}

minecraft {
	version = "${config.minecraft.version}-${config.forge.version}"
	runDir = "eclipse"

	replaceIn "pl/asie/computronics/Computronics.java"
	replace "@VERSION@", "${config.computronics.version}-Ctyrk4-v${config.ctyrk4.version}"
}

processResources {
	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		// replace version and mcversion
		expand 'version': project.version, 'mcversion': project.minecraft.version
	}

	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
	// Move access transformer to META-INF
	rename '(.+_at.cfg)', 'META-INF/$1'
}

def commonManifest = {
	attributes FMLAT: "tts_at.cfg"
}

//For Building

jar {
	exclude 'cofh/**'
	manifest commonManifest
}

def libDir = './build/libs'

task devJar ( type: Zip, dependsOn: jar ) {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	extension = 'jar'

	classifier = 'dev'
}

task deobfJar(type: Jar) {
	description = 'Creates a jar file containing the non-obfuscated class files'
	classifier = 'deobf'
	destinationDir = file(libDir)
	from sourceSets.main.output.classesDirs
	exclude 'cofh/**'
	manifest commonManifest
}

task apiZip(type: Zip) {
	description = 'Creates a zip file containing the API source files only'
	classifier = 'api'
	destinationDir = file(libDir)
	from sourceSets.main.allSource
	include 'pl/asie/**/api/**'
}

task makeJars {
	description = 'Creates the mod files'
}

makeJars.dependsOn clean, build, deobfJar, apiZip
build.shouldRunAfter clean
deobfJar.shouldRunAfter build
apiZip.shouldRunAfter deobfJar
